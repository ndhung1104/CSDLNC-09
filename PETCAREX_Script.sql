-- DATABASE CREATION
USE master;
GO

IF DB_ID('PETCAREX') IS NOT NULL
BEGIN
    ALTER DATABASE PETCAREX SET SINGLE_USER WITH ROLLBACK IMMEDIATE;
    DROP DATABASE PETCAREX;
END
GO

CREATE DATABASE PETCAREX;
GO

USE PETCAREX;
GO


-- TABLES CREATION
CREATE TABLE CUSTOMER (
	CUSTOMER_ID INT IDENTITY (1, 1) PRIMARY KEY,
	MEMBERSHIP_RANK_ID INT NOT NULL,
	CUSTOMER_NAME NVARCHAR(50) NOT NULL,
	CUSTOMER_PHONE CHAR(10),
	CUSTOMER_EMAIL VARCHAR(100) UNIQUE NOT NULL,
	CUSTOMER_PASSWORD CHAR(50) NOT NULL,
	CUSTOMER_GENDER NVARCHAR(4) NOT NULL,
	CUSTOMER_BIRTHDATE DATE,
	CUSTOMER_LOYALTY INT NOT NULL DEFAULT 0
) -- DONE
GO

CREATE TABLE CUSTOMER_SPENDING (
	CUSTOMER_ID INT NOT NULL,
	YEAR INT NOT NULL,
	MONEY_SPENT DECIMAL (18, 0) NOT NULL DEFAULT 0,
	PRIMARY KEY (CUSTOMER_ID, YEAR)
) -- DONE
GO

CREATE TABLE MEMBERSHIP_RANK (
	MEMBERSHIP_RANK_ID INT IDENTITY (1, 1) PRIMARY KEY,
	MEMBERSHIP_RANK_NAME NVARCHAR(20) NOT NULL,
	MEMBERSHIP_RANK_MAINTAIN_THRESHOLD DECIMAL (18, 0) NOT NULL,
	MEMBERSHIP_RANK_UPGRADE_CONDITION DECIMAL (18, 0) NOT NULL
) -- DONE
GO

CREATE TABLE PET (
	PET_ID INT IDENTITY (1, 1) PRIMARY KEY,
	CUSTOMER_ID INT NOT NULL,
	PET_NAME NVARCHAR(20) NOT NULL,
	PET_BREED_ID INT NOT NULL,
	PET_GENDER NVARCHAR(4) NOT NULL,
	PET_BIRTHDATE DATE,
	PET_HEALTH_STATUS NVARCHAR(20) NOT NULL
) -- DONE
GO

CREATE TABLE PET_BREED (
	BREED_ID INT IDENTITY (1, 1) PRIMARY KEY,
	BREED_NAME NVARCHAR (50) NOT NULL,
	TYPE_OF_PET NVARCHAR (50) NOT NULL
) -- DONE
GO

CREATE TABLE BRANCH (
	BRANCH_ID INT IDENTITY (1, 1) PRIMARY KEY,
	BRANCH_NAME NVARCHAR(50) NOT NULL,
	BRANCH_ADDRESS NVARCHAR(100) NOT NULL,
	BRANCH_PHONE CHAR(10) NOT NULL,
	BRANCH_OPEN_TIME TIME NOT NULL,
	BRANCH_CLOSED_TIME TIME NOT NULL,
	BRANCH_MANAGER INT DEFAULT NULL,
)

GO

CREATE TABLE EMPLOYEE (
	EMPLOYEE_ID INT IDENTITY (1, 1) PRIMARY KEY,
	CCCD CHAR(12) NOT NULL UNIQUE,
	EMPLOYEE_NAME NVARCHAR(50) NOT NULL,
	EMPLOYEE_GENDER NVARCHAR(4) NOT NULL,
	EMPLOYEE_BIRTHDATE DATE NOT NULL,
	EMPLOYEE_EMAIL VARCHAR(100) UNIQUE NOT NULL,
	EMPLOYEE_PASSWORD CHAR(50) NOT NULL,
	EMPLOYEE_JOIN_DATE DATE NOT NULL,
	EMPLOYEE_SALARY DECIMAL (18, 0) NOT NULL,
	EMPLOYEE_POSITION CHAR(10) NOT NULL,
	BRANCH_ID INT NOT NULL,
)
GO

CREATE TABLE TRANSFER_HISTORY (
	TRANSFER_HISTORY_ID INT IDENTITY (1, 1) PRIMARY KEY,
	EMPLOYEE_ID INT NOT NULL,
	FROM_BRANCH_ID INT NOT NULL,
	DEST_BRANCH_ID INT NOT NULL,
	TRANSFER_DATE DATE NOT NULL DEFAULT GETDATE(),
)
GO

CREATE TABLE SERVICE (
	SERVICE_ID INT IDENTITY (1, 1) PRIMARY KEY,
	SERVICE_NAME NVARCHAR(30) NOT NULL,
)
GO

CREATE TABLE BRANCH_SERVICE (
	BRANCH_ID INT NOT NULL,
	SERVICE_ID INT NOT NULL,
	PRIMARY KEY (BRANCH_ID, SERVICE_ID)
)
GO

CREATE TABLE APPOINTMENT (
	APPOINTMENT_ID INT IDENTITY (1, 1) PRIMARY KEY,
	CUSTOMER_ID INT NOT NULL,
	BRANCH_ID INT NOT NULL,
	SERVICE_ID INT NOT NULL,
	APPOINTMENT_CREATE_DATE DATETIME NOT NULL,
	APPOINTMENT_DATE DATETIME NOT NULL,
	APPOINTMENT_STATUS NVARCHAR(50) NOT NULL,
) 
GO

CREATE TRIGGER TRG_APPOINTMENT_LIMIT_PER_SLOT
ON APPOINTMENT
AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON;

	IF EXISTS (
		SELECT 1
		FROM (
			SELECT BRANCH_ID, APPOINTMENT_DATE
			FROM inserted
			GROUP BY BRANCH_ID, APPOINTMENT_DATE
		) i
		JOIN (
			SELECT BRANCH_ID, APPOINTMENT_DATE, COUNT(*) AS total
			FROM APPOINTMENT
			GROUP BY BRANCH_ID, APPOINTMENT_DATE
		) a
			ON a.BRANCH_ID = i.BRANCH_ID AND a.APPOINTMENT_DATE = i.APPOINTMENT_DATE
		WHERE a.total > 5
	)
	BEGIN
		RAISERROR ('Each branch and time slot can only have 5 appointments.', 16, 1);
		ROLLBACK TRANSACTION;
		RETURN;
	END
END
GO

CREATE TABLE PRODUCT (
	PRODUCT_ID INT IDENTITY (1, 1) PRIMARY KEY,
	PRODUCT_NAME NVARCHAR(100) NOT NULL
)
GO

CREATE TABLE SALES_PRODUCT (
	SALES_PRODUCT_ID INT PRIMARY KEY,
	SALES_PRODUCT_PRICE DECIMAL (18, 0) NOT NULL
)
GO

CREATE TABLE BRANCH_STOCK (
	BRANCH_ID INT NOT NULL,
	SALES_PRODUCT_ID INT NOT NULL,
	QUANTITY INT DEFAULT 0 NOT NULL,
	PRIMARY KEY (BRANCH_ID, SALES_PRODUCT_ID)
)
GO

CREATE TABLE MEDICAL_SERVICE (
	MEDICAL_SERVICE_ID INT PRIMARY KEY,
	MEDICAL_SERVICE_FEE DECIMAL (18, 0) NOT NULL
)
GO

CREATE TABLE VACCINATION_PLAN (
	VACCINATION_PLAN_ID INT PRIMARY KEY,
	VACCINATION_PLAN_DURATION INT NOT NULL,
	VACCINATION_PLAN_PRICE DECIMAL (18, 0) NOT NULL
)
GO

CREATE TABLE VACCINATION_PLAN_DETAIL (
	VACCINATION_PLAN_ID INT NOT NULL,
	VACCINE_ID INT NOT NULL,
	VACCINE_DOSAGE INT NOT NULL,
	PRIMARY KEY (VACCINATION_PLAN_ID, VACCINE_ID)
)
GO

CREATE TABLE PET_VACCINATION_PLAN (
	PET_VACCINATION_PLAN_ID INT IDENTITY (1, 1) PRIMARY KEY,
	PET_ID INT NOT NULL,
	VACCINATION_PLAN_ID INT NOT NULL,
	PLAN_START_DATE DATE NOT NULL DEFAULT GETDATE(),
	PLAN_END_DATE DATE NOT NULL
)
GO

CREATE TABLE CHECK_UP (
	CHECK_UP_ID INT IDENTITY (1, 1) PRIMARY KEY,
	MEDICAL_SERVICE INT NOT NULL,
	PET_ID INT NOT NULL,
	VET_ID INT NOT NULL,
	SYMPTOMS NVARCHAR(MAX),
	DIAGNOSIS NVARCHAR(MAX),
	PRESCRIPTION_AVAILABLE BIT NOT NULL DEFAULT 0,
	FOLLOW_UP_VISIT DATETIME,
	STATUS NVARCHAR(10) NOT NULL DEFAULT N'Chờ'
) -- DONE
GO

CREATE TABLE PRESCRIPTION_DETAIL (
	CHECK_UP_ID INT NOT NULL,
	PRESCRIPTION_NUMBER INT NOT NULL,
	PRODUCT_ID INT NOT NULL,
	QUANTITY INT NOT NULL,
	PRIMARY KEY (CHECK_UP_ID, PRESCRIPTION_NUMBER)
)
GO

CREATE TABLE VACCINATION (
	VACCINATION_ID INT IDENTITY (1, 1) PRIMARY KEY,
	MEDICAL_SERVICE INT NOT NULL,
	PET_ID INT NOT NULL,
	VET_ID INT NOT NULL,
	VACCINE_BATCH_ID INT NOT NULL,
	PET_VACCINATION_PLAN_ID INT,
	VACCINATION_DOSAGE NVARCHAR(50)
)
GO

CREATE TABLE VACCINE (
	VACCINE_ID INT IDENTITY (1, 1) PRIMARY KEY,
	VACCINE_NAME NVARCHAR (50) NOT NULL,
	VACCINE_DES NVARCHAR (MAX),
	VACCINE_PRICE DECIMAL (18, 0) NOT NULL
)
GO

CREATE TABLE BRANCH_VACCINE_BATCH (
	VACCINE_BATCH_ID INT IDENTITY (1, 1) PRIMARY KEY,
	VACCINE_ID INT NOT NULL,
	BRANCH_ID INT NOT NULL,
	VACCINE_BATCH_MFD DATE NOT NULL,
	VACCINE_BATCH_EXP DATE NOT NULL,
	VACCINE_BATCH_QUANTITY INT NOT NULL
)
GO

CREATE TABLE RECEIPT (
	RECEIPT_ID INT IDENTITY (1, 1) PRIMARY KEY,
	BRANCH_ID INT NOT NULL,
	CUSTOMER_ID INT NOT NULL,
	RECEPTIONIST_ID INT NOT NULL,
	RECEIPT_CREATED_DATE DATETIME NOT NULL DEFAULT GETDATE(),
	RECEIPT_TOTAL_PRICE DECIMAL (18, 0) NOT NULL,
	RECEIPT_PAYMENT_METHOD NVARCHAR(20) NOT NULL,
	RECEIPT_STATUS NVARCHAR(20) NOT NULL
)
GO

CREATE TABLE RECEIPT_DETAIL (
	RECEIPT_ITEM_ID INT NOT NULL,
	RECEIPT_ID INT NOT NULL,
	PRODUCT_ID INT NOT NULL,
	PET_ID INT,
	RECEIPT_ITEM_AMOUNT INT NOT NULL DEFAULT 1,
	RECEIPT_ITEM_PRICE DECIMAL (18, 0) NOT NULL,
	PRIMARY KEY (RECEIPT_ITEM_ID, RECEIPT_ID)
)
GO

CREATE TABLE REVIEW (
	RECEIPT_ID INT PRIMARY KEY,
	SERVICE_SCORE INT NOT NULL DEFAULT 0,
	STAFF_SCORE INT NOT NULL DEFAULT 0,
	OVERALL_SCORE INT NOT NULL DEFAULT 0,
	COMMENT NVARCHAR(MAX),
)
GO


-- DOMAIN CONSTRAINTS & FOREIGN KEY
ALTER TABLE CUSTOMER ADD
	CONSTRAINT FK_CUSTOMER_MEMBERSHIP FOREIGN KEY (MEMBERSHIP_RANK_ID) REFERENCES MEMBERSHIP_RANK (MEMBERSHIP_RANK_ID),
	CONSTRAINT CK_CUSTOMER_GENDER CHECK (CUSTOMER_GENDER IN (N'Nam', N'Nữ')),
	CONSTRAINT CK_CUSTOMER_LOYALTY CHECK (CUSTOMER_LOYALTY >= 0)
GO

ALTER TABLE CUSTOMER_SPENDING ADD
	CONSTRAINT FK_SPENDING_CUSTOMER FOREIGN KEY (CUSTOMER_ID) REFERENCES CUSTOMER (CUSTOMER_ID),
	CONSTRAINT CK_SPENDING_YEAR CHECK (YEAR >= 0),
	CONSTRAINT CK_SPENDING_MONEYSPENT CHECK (MONEY_SPENT >= 0)
GO

ALTER TABLE MEMBERSHIP_RANK ADD
	CONSTRAINT CK_MEMBERSHIP_MAINTAIN CHECK (MEMBERSHIP_RANK_MAINTAIN_THRESHOLD >= 0),
	CONSTRAINT CK_MEMBERSHIP_UPGRADE CHECK (MEMBERSHIP_RANK_UPGRADE_CONDITION >= 0),
	CONSTRAINT CK_MEMBERSHIP_MAINTAIN_UPGRADE CHECK (MEMBERSHIP_RANK_MAINTAIN_THRESHOLD < MEMBERSHIP_RANK_UPGRADE_CONDITION)
GO

ALTER TABLE PET ADD
	CONSTRAINT FK_PET_CUSTOMER FOREIGN KEY (CUSTOMER_ID) REFERENCES CUSTOMER (CUSTOMER_ID),
	CONSTRAINT FK_PET_PBREED FOREIGN KEY (PET_BREED_ID) REFERENCES PET_BREED (BREED_ID),
	CONSTRAINT CK_PET_GENDER CHECK (PET_GENDER IN (N'Nam', N'Nữ'))
GO

ALTER TABLE BRANCH ADD
	CONSTRAINT FK_BRANCH_EMPLOYEE FOREIGN KEY (BRANCH_MANAGER) REFERENCES EMPLOYEE (EMPLOYEE_ID)
GO

ALTER TABLE EMPLOYEE ADD
	CONSTRAINT FK_EMPLOYEE_BRANCH FOREIGN KEY (BRANCH_ID) REFERENCES BRANCH (BRANCH_ID),
	CONSTRAINT CK_EMPLOYEE_GENDER CHECK (EMPLOYEE_GENDER IN (N'Nam', N'Nữ')),
	CONSTRAINT CK_EMPLOYEE_SALARY CHECK (EMPLOYEE_SALARY >= 0)
GO

ALTER TABLE TRANSFER_HISTORY ADD
	CONSTRAINT FK_TRANSFER_EMPLOYEE FOREIGN KEY (EMPLOYEE_ID) REFERENCES EMPLOYEE (EMPLOYEE_ID),
	CONSTRAINT FK_TRANSFER_BRANCH_FRM FOREIGN KEY (FROM_BRANCH_ID) REFERENCES BRANCH (BRANCH_ID),
	CONSTRAINT FK_TRANSFER_BRANCH_DES FOREIGN KEY (DEST_BRANCH_ID) REFERENCES BRANCH (BRANCH_ID)
GO

ALTER TABLE SALES_PRODUCT ADD
	CONSTRAINT FK_SALESPRODUCT_PRODUCT FOREIGN KEY (SALES_PRODUCT_ID) REFERENCES PRODUCT (PRODUCT_ID),
	CONSTRAINT CK_SALESPRODUCT_PRICE CHECK (SALES_PRODUCT_PRICE >= 0)
GO

ALTER TABLE BRANCH_STOCK ADD
	CONSTRAINT FK_BRSTOCK_BRANCH FOREIGN KEY (BRANCH_ID) REFERENCES BRANCH (BRANCH_ID),
	CONSTRAINT FK_BRSTOCK_SALESPRODUCT FOREIGN KEY (SALES_PRODUCT_ID) REFERENCES SALES_PRODUCT (SALES_PRODUCT_ID),
	CONSTRAINT CK_BRSTOCK_QUANTITY CHECK (QUANTITY >= 0)
GO

ALTER TABLE BRANCH_SERVICE ADD
	CONSTRAINT FK_BRSERVICE_BRANCH FOREIGN KEY (BRANCH_ID) REFERENCES BRANCH (BRANCH_ID),
	CONSTRAINT FK_BRSERVICE_SERVICE FOREIGN KEY (SERVICE_ID) REFERENCES SERVICE (SERVICE_ID)
GO

ALTER TABLE APPOINTMENT ADD
	CONSTRAINT FK_APPOINTMENT_CUSTOMER FOREIGN KEY (CUSTOMER_ID) REFERENCES CUSTOMER (CUSTOMER_ID),
	CONSTRAINT FK_APPOINTMENT_BRSERVICE FOREIGN KEY (BRANCH_ID, SERVICE_ID) REFERENCES BRANCH_SERVICE (BRANCH_ID, SERVICE_ID),
	CONSTRAINT CK_APPOINTMENT_CREATEDATE_APPDATE CHECK (APPOINTMENT_CREATE_DATE <= APPOINTMENT_DATE)
GO

ALTER TABLE MEDICAL_SERVICE ADD
	CONSTRAINT FK_MSERVICE_PRODUCT FOREIGN KEY (MEDICAL_SERVICE_ID) REFERENCES PRODUCT (PRODUCT_ID),
	CONSTRAINT CK_MSERVICE_FEE CHECK (MEDICAL_SERVICE_FEE >= 0)
GO

ALTER TABLE VACCINATION_PLAN ADD
	CONSTRAINT FK_VACCINATIONPLAN_PRODUCT FOREIGN KEY (VACCINATION_PLAN_ID) REFERENCES PRODUCT (PRODUCT_ID),
	CONSTRAINT CK_VACCINATIONPLAN_DURATION CHECK (VACCINATION_PLAN_DURATION >= 0),
	CONSTRAINT CK_VACCINATIONPLAN_PRICE CHECK (VACCINATION_PLAN_PRICE >= 0)
GO

ALTER TABLE VACCINATION_PLAN_DETAIL ADD
	CONSTRAINT FK_VACCINEPLANDETAIL_VACCINATIONPLAN FOREIGN KEY (VACCINATION_PLAN_ID) REFERENCES VACCINATION_PLAN (VACCINATION_PLAN_ID),
	CONSTRAINT FK_VACCINEPLANDETAIL_VACCINE FOREIGN KEY (VACCINE_ID) REFERENCES VACCINE (VACCINE_ID),
	CONSTRAINT CK_VACCINEPLANDETAIL_DOSAGE CHECK (VACCINE_DOSAGE >= 0)
GO

ALTER TABLE VACCINE ADD
	CONSTRAINT CK_VACCINE_PRICE CHECK (VACCINE_PRICE >= 0)
GO

ALTER TABLE PET_VACCINATION_PLAN ADD
	CONSTRAINT FK_PETVACCINEPLAN_PET FOREIGN KEY (PET_ID) REFERENCES PET (PET_ID),
	CONSTRAINT FK_PETVACCINEPLAN_VACCINATIONPLAN FOREIGN KEY (VACCINATION_PLAN_ID) REFERENCES VACCINATION_PLAN (VACCINATION_PLAN_ID),
	CONSTRAINT CK_PETVACCINEPLAN_STARTDATE_ENDDATE CHECK (PLAN_START_DATE <= PLAN_END_DATE)
GO

ALTER TABLE CHECK_UP ADD
	CONSTRAINT FK_CHECKUP_MSERVICE FOREIGN KEY (MEDICAL_SERVICE) REFERENCES MEDICAL_SERVICE (MEDICAL_SERVICE_ID),
	CONSTRAINT FK_CHECKUP_PET FOREIGN KEY (PET_ID) REFERENCES PET (PET_ID),
	CONSTRAINT FK_CHECKUP_EMPLOYEE FOREIGN KEY (VET_ID) REFERENCES EMPLOYEE (EMPLOYEE_ID)
GO

ALTER TABLE PRESCRIPTION_DETAIL ADD
	CONSTRAINT FK_PRESCRIPTION_CHECKUP FOREIGN KEY (CHECK_UP_ID) REFERENCES CHECK_UP (CHECK_UP_ID),
	CONSTRAINT FK_PRESCRIPTION_SALESPRODUCT FOREIGN KEY (PRODUCT_ID) REFERENCES SALES_PRODUCT (SALES_PRODUCT_ID),
	CONSTRAINT CK_PRESCRIPTION_QUANTITY CHECK (QUANTITY >= 0)
GO

ALTER TABLE VACCINATION ADD
	CONSTRAINT FK_VACCINATION_MSERVICE FOREIGN KEY (MEDICAL_SERVICE) REFERENCES MEDICAL_SERVICE (MEDICAL_SERVICE_ID),
	CONSTRAINT FK_VACCINATION_VACCINEBATCH FOREIGN KEY (VACCINE_BATCH_ID) REFERENCES BRANCH_VACCINE_BATCH (VACCINE_BATCH_ID),
	CONSTRAINT FK_VACCINATION_PETPLAN FOREIGN KEY (PET_VACCINATION_PLAN_ID) REFERENCES PET_VACCINATION_PLAN (PET_VACCINATION_PLAN_ID),
	CONSTRAINT FK_VACCINATION_PET FOREIGN KEY (PET_ID) REFERENCES PET (PET_ID),
	CONSTRAINT FK_VACCINATION_EMPLOYEE FOREIGN KEY (VET_ID) REFERENCES EMPLOYEE (EMPLOYEE_ID)
GO

ALTER TABLE BRANCH_VACCINE_BATCH ADD
	CONSTRAINT FK_VACCINEBATCH_VACCINE FOREIGN KEY (VACCINE_ID) REFERENCES VACCINE (VACCINE_ID),
	CONSTRAINT FK_VACCINEBATCH_BRANCH FOREIGN KEY (BRANCH_ID) REFERENCES BRANCH (BRANCH_ID),
	CONSTRAINT CK_VACCINEBATCH_QUANTITY CHECK (VACCINE_BATCH_QUANTITY >= 0),
	CONSTRAINT CK_VACCINEBATCH_MFD_EXP CHECK (VACCINE_BATCH_MFD <= VACCINE_BATCH_EXP)
GO

ALTER TABLE RECEIPT ADD
	CONSTRAINT FK_RECEIPT_BRANCH FOREIGN KEY (BRANCH_ID) REFERENCES BRANCH (BRANCH_ID),
	CONSTRAINT FK_RECEIPT_CUSTOMER FOREIGN KEY (CUSTOMER_ID) REFERENCES CUSTOMER (CUSTOMER_ID),
	CONSTRAINT FK_RECEIPT_EMPLOYEE FOREIGN KEY (RECEPTIONIST_ID) REFERENCES EMPLOYEE (EMPLOYEE_ID),
	CONSTRAINT CK_RECEIPT_TOTALPRICE CHECK (RECEIPT_TOTAL_PRICE >= 0)
GO

ALTER TABLE RECEIPT_DETAIL ADD
	CONSTRAINT FK_RECEIPTDETAIL_RECEIPT FOREIGN KEY (RECEIPT_ID) REFERENCES RECEIPT (RECEIPT_ID),
	CONSTRAINT FK_RECEIPTDETAIL_PRODUCT FOREIGN KEY (PRODUCT_ID) REFERENCES PRODUCT (PRODUCT_ID),
	CONSTRAINT FK_RECEIPTDETAIL_PET FOREIGN KEY (PET_ID) REFERENCES PET (PET_ID),
	CONSTRAINT CK_RECEIPTDETAIL_AMOUNT CHECK (RECEIPT_ITEM_AMOUNT >= 1),
	CONSTRAINT CK_RECEIPTDETAIL_PRICE CHECK (RECEIPT_ITEM_PRICE >= 0)
GO

ALTER TABLE REVIEW ADD
	CONSTRAINT FK_REVIEW_RECEIPT FOREIGN KEY (RECEIPT_ID) REFERENCES RECEIPT (RECEIPT_ID) ON DELETE CASCADE,
	CONSTRAINT CK_REVIEW_SERVICESCORE CHECK (SERVICE_SCORE >= 0 AND SERVICE_SCORE <= 10),
	CONSTRAINT CK_REVIEW_STAFFSCORE CHECK (STAFF_SCORE >= 0 AND STAFF_SCORE <= 10),
	CONSTRAINT CK_REVIEW_OVERALLSCORE CHECK (OVERALL_SCORE >= 0 AND OVERALL_SCORE <= 10)
GO
